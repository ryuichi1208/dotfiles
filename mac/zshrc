if [ $(tput cols) -gt 180 ]; then
  echo "\e[1;32m"
  cat << 'EOF'
       (gQQQf~`+1<<111<<~
     .JmgggK` ,>?>>?><!
   .Jmgmgm@` ,>>?>><!   ....,  .....          .....  ...........
 .Jmggggg@` ,>>>?<!    .MMMMF  UMMMMp        .MMMMF  JMMMMMMMMMMMNJ
Jmgggmgg@` ,>>><!      .MMMMF   WMMMMx      .MMMM#   JMMMMMMMMMMMMMN.
                       .MMMMF    MMMMN,    .MMMMM`   JMMMM_    ?MMMMb  MMMMM,       (MMMMD
                       .MMMMF    .MMMMN.  .MMMMM'    JMMMM_    .MMMMF  .MMMMN,     .MMMMF.
                       .MMMMF     ,MMMMN  dMMMM%     JMMMMMMMMMMMMMM!   ,MMMMN.   .MMMM@
                       .MMMMF      JMMMMb(MMMMF      JMMMMMMMMMMMMF      (MMMMN  .MMMM#`
                       .MMMMF       TMMMMMMMMF       JMMMM=???MMMMN.      ?MMMMb.MMMMM!
                       .MMMMF        WMMMMMM@        JMMMM~   (MMMMN       UMMMMMMMMM^
                       .MMMMF         MMMMM#`        JMMMM~    ?MMMMb       WMMMMMMM$
                                                                             MMMMMMF
                                                                             .MMMMF
                                                                            .MMMM#
                                                                           MMMMM
EOF
fi

###############
### brew
###############
export HOMEBREW_PREFIX="/opt/homebrew";
export HOMEBREW_CELLAR="/opt/homebrew/Cellar";
export HOMEBREW_REPOSITORY="/opt/homebrew";
export PATH="${HOME}/.local/share/mise/shims:${HOME}/.pyenv/shims:/opt/homebrew/sbin:/System/Volumes/Data/opt/homebrew/bin:${PATH}"
[ -z "${MANPATH-}" ] || export MANPATH=":${MANPATH#:}";
export INFOPATH="/opt/homebrew/share/info:${INFOPATH:-}";

###############
### basic
###############
HISTFILE=~/.zsh_history
HISTSIZE=1000000
SAVEHIST=1000000
setopt share_history
setopt hist_ignore_all_dups
setopt hist_ignore_space
setopt hist_reduce_blanks
setopt hist_save_no_dups
setopt inc_append_history
setopt no_beep
setopt nolistbeep
autoload -Uz select-word-style
select-word-style default 
zstyle ':zle:*' word-chars ' /=;@:{}[]()<>,|.'
zstyle ':zle:*' word-style unspecified

# ファイルがあればsourceする
if [ -f ~/.zshrc.local ]; then
  source ~/.zshrc.local
fi

###############
### alias
###############
alias ..="cd .."
alias v="vim"
alias l="ls"
alias ls="eza"
alias ll="ls -l"
alias la="ls -la"
alias python="python3"
alias g="git"
alias gb="git branch"
alias gd="git diff"
alias gs="git status"
alias gp="git pull"
alias c="powered_cd"
alias d="docker"
alias di="docker image"
alias dc="docker container"
alias dm="docker-compose"
alias tf="terraform"

###############
### zsh-completions
###############
if [ -e /usr/local/share/zsh-completions ]; then
  fpath=(/usr/local/share/zsh-completions $fpath)
fi
fpath=($fpath ~/.zsh/completion)

autoload -Uz compinit
compinit -u
zstyle ':completion:*' matcher-list 'm:{a-z}={A-Z}'
zstyle ':completion:*' ignore-parents parent pwd ..
zstyle ':completion:*:default' menu select=1
zstyle ':completion:*:cd:*' ignore-parents parent pwd
zstyle ":completion:*:commands" rehash 1

###############
### plugin
###############
source ~/ghq/github.com/zsh-users/zsh-autosuggestions/zsh-autosuggestions.zsh
source $(brew --prefix)/opt/zsh-git-prompt/zshrc.sh
source $(brew --prefix)/share/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh

###############
### mise
###############
eval "$(~/.local/bin/mise activate zsh)"

###############
### fzf
###############
export FZF_DEFAULT_OPTS="
    --height 90% --reverse --border
    --prompt='➜  ' --margin=0,1 --inline-info
    --tiebreak=index --no-mouse --filepath-word
    --color fg:-1,bg:-1,hl:33,fg+:250,bg+:235,hl+:33
    --color info:37,prompt:37,pointer:230,marker:230,spinner:37
    --bind='ctrl-w:backward-kill-word,ctrl-x:jump,down:preview-page-down'
    --bind='ctrl-z:ignore,ctrl-]:replace-query,up:preview-page-up'
    --bind='ctrl-a:toggle-all,?:toggle-preview'
"

###############
### function
###############
function git_prompt() {
  if [ "$(git rev-parse --is-inside-work-tree 2> /dev/null)" = true ]; then
    PROMPT="%F{green}%n%f %F{cyan}($(arch))%f:%F{blue}%~%f $(git_super_status)"$'\n'"%# "
  else
    PROMPT="%F{green}%n%f %F{cyan}($(arch))%f:%F{blue}%~%f"$'\n'"%# "
  fi
}

function precmd() {
  git_prompt
}

function fd() {
  local dir
  dir=$(find ${1:-.} -path '*/\.*' -prune \
                  -o -type d -print 2> /dev/null | fzf +m) &&
  cd "$dir"
}

function fbr() {
  local branches branch
  branches=$(git branch --all | grep -v HEAD) &&
  branch=$(echo "$branches" |
           fzf-tmux -d $(( 2 + $(wc -l <<< "$branches") )) +m) &&
  git checkout $(echo "$branch" | sed "s/.* //" | sed "s#remotes/[^/]*/##")
}
zle -N fbr
bindkey '^f' fbr

function fzf-ghq() {
  local repo=$(ghq list \
	| fzf --color --preview "ghq list --full-path --exact {} \
	| xargs eza -h --long --classify --git --no-permissions --no-user --no-filesize --git-ignore --sort modified --reverse --tree --level 2"
  )
  if [ -n "$repo" ]; then
    repo=$(ghq list --full-path --exact $repo)
    BUFFER="cd ${repo}"
    zle accept-line
  fi
  zle clear-screen
}
zle -N fzf-ghq
bindkey '^g' fzf-ghq

function gaa() {
  MSG=$1
  if [ -z ${MSG} ]; then
    echo "please commit message input!!"
    exit 1
  fi
  git add . && git commit -m ${MSG} && git push
}
export OBJC_DISABLE_INITIALIZE_FORK_SAFETY=YES

function fzf-select-history() {
    BUFFER=$(history -n -r 1 | fzf --query "$LBUFFER" --reverse)
    CURSOR=$#BUFFER
    zle reset-prompt
}
zle -N fzf-select-history
bindkey '^r' fzf-select-history

function awsp() {
   PROFILES=$(aws configure list-profiles)
   SELECTED_PROFILE=$(echo "$PROFILES" | fzf)

   if [[ -n "$SELECTED_PROFILE" ]]; then
      export AWS_PROFILE="$SELECTED_PROFILE"
      echo "Updated profile to $AWS_PROFILE"
   else
      echo "No profile selected"
   fi
}
zle -N awsp
bindkey '^p' awsp

alias cdg=git_root

function git_root() {
    # Check if the current directory is inside a git repository
    if git rev-parse --show-toplevel > /dev/null 2>&1; then
        # Move to the git repository root directory
        cd "$(git rev-parse --show-toplevel)"
        echo "Moved to git root: $(pwd)"
    else
        echo "Not inside a git repository"
    fi
}

function chpwd() {
  powered_cd_add_log
}

function powered_cd_add_log() {
  local i=0
  cat ~/.powered_cd.log | while read line; do
    (( i++ ))
    if [ i = 30 ]; then
      sed -i -e "30,30d" ~/.powered_cd.log
    elif [ "$line" = "$PWD" ]; then
      sed -i -e "${i},${i}d" ~/.powered_cd.log
    fi
  done
  echo "$PWD" >> ~/.powered_cd.log
}

function powered_cd() {
  if [ $# = 0 ]; then
    cd $(gtac ~/.powered_cd.log | fzf)
  elif [ $# = 1 ]; then
    cd $1
  else
    echo "powered_cd: too many arguments"
  fi
  pwd; ls
}
_powered_cd() {
  _files -/
}
compdef _powered_cd powered_cd
[ -e ~/.powered_cd.log ] || touch ~/.powered_cd.log
zle -N powered_cd
bindkey '^u' powered_cd

autoload bashcompinit && bashcompinit
autoload -Uz compinit && compinit
complete -C '/usr/local/bin/aws_completer' aws

_cdktf_yargs_completions()
{
  local reply
  local si=$IFS
  IFS=$'
' reply=($(COMP_CWORD="$((CURRENT-1))" COMP_LINE="$BUFFER" COMP_POINT="$CURSOR" /Users/ryuichi/.local/share/mise/installs/node/latest/bin/cdktf --get-yargs-completions "${words[@]}"))
  IFS=$si
  _describe 'values' reply
}
compdef _cdktf_yargs_completions cdktf

function fzf-repo() {
  local repo=$(ghq list \
        | fzf --color --preview "ghq list --full-path --exact {} \
        | xargs eza -h --long --classify --git --no-permissions --no-user --no-filesize --git-ignore --sort modified --reverse --tree --level 2"
  )
  (
    if [ -n "$repo" ]; then
      repo=$(ghq list --full-path --exact $repo)
      cd ${repo} && hub browse
      zle accept-line
    fi
  )
  zle clear-screen
}
zle -N fzf-repo
bindkey '^b' fzf-repo

